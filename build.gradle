plugins {
    id 'java'
    id 'com.vaadin'
    id 'io.freefair.lombok'
    id 'io.spring.dependency-management'
}

repositories {
    mavenLocal()
    mavenCentral()
}

apply from: "$rootDir/tasks/revisions.gradle"
dependencyManagement {
    imports {
        mavenBom "io.sunshower.aire:bom-imported:$version"
        mavenBom "io.sunshower.aire:bom-exported:$version"
    }
}


sourceSets {
    main {
        java {
            srcDir "${buildDir}/generated/sources/main/java"
        }
    }
    development {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
        }
    }

}


task populateVersions(type: Copy) {
    from "${project.projectDir}/src/main/resources/templates/Versions.java"
    into "${buildDir}/generated/sources/main/java/io/sunshower/aire/components"

    def properties = new Properties()
    file("gradle.properties").withInputStream {
        properties.load(it)
    }
    def devMode = findProperty('development') != null
    if (devMode) {
        properties.replaceAll((k, v) -> {
            def sv = String.valueOf(v)
            return sv.contains("-SNAPSHOT") ? sv.replaceAll("-SNAPSHOT", "") : sv
        })
    }
    filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: properties)
}

build.dependsOn populateVersions
build.mustRunAfter populateVersions

configurations {
    developmentImplementation {
        extendsFrom implementation
    }
}

dependencies {
    [
            'com.vaadin:flow-server',
            'com.vaadin:vaadin-icons-flow',
            'io.sunshower.aire:aire-common',
            'com.vaadin:flow-html-components',
            'io.sunshower.arcus:arcus-lang',
            'io.sunshower.arcus:arcus-reflect'
    ].each {
        implementation it
        developmentImplementation it
    }

    developmentImplementation 'com.vaadin:vaadin-spring-boot-starter'


}
